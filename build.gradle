buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:latest.release'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:latest.release'
    }
}

apply plugin: 'groovy'
apply plugin: 'android-maven'
apply plugin: 'com.jfrog.bintray'

group = "com.tencent.bugly"
version = "1.3.6"

def symtabToolVersion = '2.3.1'
def buglySymbolAndroidJar = "libs/buglySymbolAndroid${symtabToolVersion}.jar"

dependencies {
    repositories {
        jcenter()
    }
    compile gradleApi()
    compile localGroovy()
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'org.apache.httpcomponents:httpclient:4.3.6'
    compile 'org.apache.httpcomponents:httpmime:4.3.6'
    compile 'com.android.tools.build:gradle:latest.release'
}

def siteUrl = "https://github.com/JalenChen/SymtabFileUploader"
def gitUrl = "https://github.com/JalenChen/SymtabFileUploader.git"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            // packaging 'jar'
            // Add your description here
            name = 'Uploading mapping file and symtab file which is extracted from debug so file'
            url siteUrl
            // Set your license
            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'jalenchen'
                    name = 'wenkechen'
                    email 'jalenchen@qq.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task unzipSymbolToolJarToClasses(type: Copy) {
    from zipTree(buglySymbolAndroidJar)
    exclude 'META-INF'
    into 'build/classes/main/'
}

jar.dependsOn unzipSymbolToolJarToClasses

task sourcesJar(type: Jar) {
    // from zipTree(buglyPluginJar)
    from 'src/main/groovy'
    exclude 'META-INF'
    classifier = 'sources'
}

groovydoc {
    includePrivate = true
    source = 'src/main/groovy'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives groovydocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    pkg {
        repo = "maven"
        name = "SymtabFileUploader"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}